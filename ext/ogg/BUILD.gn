declare_args() {
  if (target_os == "linux") {
    ogg = "system"
  } else {
    ogg = "bundled"
  }
}
assert(ogg == "system" || ogg == "bundled")

if (ogg == "bundled") {
  static_library("ogg") {
    sources = [
      "libogg-1.3.0/src/bitwise.c",
      "libogg-1.3.0/src/framing.c",
    ]
    configs += [ ":ogg_private" ]
    public_configs = [ ":ogg_public" ]
  }

  config("ogg_private") {
    include_dirs = [ "libogg-1.3.0/include" ]
    if (target_os == "mac") {
      include_dirs += [ "darwin" ]
    }
  }

  config("ogg_public") {
    include_dirs = [ "libogg-1.3.0/include" ]
  }
} else {
  import("//build/lib/pkg_config.gni")

  pkg_config("ogg_public") {
    lib = "ogg"
  }

  source_set("ogg") {
    public_configs = [ ":ogg_public" ]
  }
}
